buildscript {
    repositories {
        jcenter()
        maven {
            url 'https://maven.google.com'
        }
        maven {
            url 'https://dl.google.com/dl/android/maven2/'
        }
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:3.0.0'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3'
        classpath 'org.jfrog.buildinfo:build-info-extractor-gradle:4.1.0'
    }
}

apply plugin: 'com.android.library'

apply from: '../scripts/version_bump.gradle'
ext.libraryArtifactId = "android-beacon-library"

allprojects {
    // version = "${getVersionName()}${isSnapshot == true ? "-SNAPSHOT" : ""}"
    group = "com.conichi.sdk"

    repositories {
        jcenter()
        maven {
            url 'https://maven.google.com'
        }
        maven {
            url 'https://dl.google.com/dl/android/maven2/'
        }
    }
}

android {
    compileSdkVersion compileSdk
    buildToolsVersion buildTools

    defaultConfig {
        // Unfortunately 'com.android.support:appcompat-v7:26.0.0'
        // requires minSdkVersion 14, forcing a bump verson minSdkVersion 7
        // But since only 0.8% of Android devices have < SDK 14 as of Une 2017, this will become
        // the new min version for this library in order to target Android O
        minSdkVersion 14
        targetSdkVersion targetSdk
        versionCode 1
        versionName version
        consumerProguardFiles 'proguard-rules.pro'
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    compileOptions {
        encoding "UTF-8"
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    lintOptions {
        abortOnError false
    }

    packagingOptions {
        exclude 'LICENSE.txt'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE'
    }

    libraryVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "${libraryArtifactId}-${gitVersionName}.aar"
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile libs.supportAppCompat
    compile libs.supportAnnotations

    testCompile('junit:junit:4.12') {
        exclude group: 'org.hamcrest'
    }
    testCompile('org.hamcrest:hamcrest-junit:2.0.0.0') {
        exclude group: 'junit'
    }
    testCompile('com.squareup:fest-android:1.0.+@aar')
    testCompile('org.robolectric:robolectric:3.0') {
        exclude group: 'junit'
    }
    testCompile('org.mockito:mockito-core:1.10.19') {
        exclude group: 'org.hamcrest'
    }
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
        exclude group: 'org.hamcrest'
    })
    androidTestCompile 'org.apache.commons:commons-math3:3.6.1'
}

apply from: 'gradle/compile.gradle'
